// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.kotlin_version = '1.3.21'

  ext.buildConfig = [
      'compileSdk': 28,
      'minSdk': 26,
      'targetSdk': 28,

      'version': [
          'major': 0,
          'minor': 1,
          'patch': 0,
          'build': 0,
      ],
  ]
  ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
  ext.buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
  ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

  ext.versions = [
      'android': '3.5.0-alpha10',
      'kotlin': '1.3.21',
      'kotlinCoroutines': '1.2.0',
      'dagger': '2.22.1',
      'sqldelight': '1.1.1',
  ]

  ext.deps = [
      'androidx': [
          'appcompat': 'androidx.appcompat:appcompat:1.0.2',
          'constraintLayout': 'androidx.constraintlayout:constraintlayout:1.1.3',
          'coreKtx': 'androidx.core:core-ktx:1.0.1',
          'test': [
              'junit': 'androidx.test.ext:junit:1.1.0',
              'espressoCore': 'androidx.test.espresso:espresso-core:3.1.1'
          ]
      ],
      'dagger': [
          'runtime': "com.google.dagger:dagger:${versions.dagger}",
          'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
          'androidRuntime': "com.google.dagger:dagger-android:${versions.dagger}",
          'androidCompiler': "com.google.dagger:dagger-android-processor:${versions.dagger}",
      ],
      'junit': 'junit:junit:4.12',
      'kotlin': [
          'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
      ],
      'moshi': 'com.squareup.moshi:moshi:1.8.0',
      'timber': [
          'common': 'com.jakewharton.timber:timber-common:5.0.0-SNAPSHOT',
          'android': 'com.jakewharton.timber:timber-android:5.0.0-SNAPSHOT',
      ],
      plugins: [
          'android': "com.android.tools.build:gradle:${versions.android}",
          'kotlin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
      ],
      'sqldelight': [
          'android': "com.squareup.sqldelight:android-driver:${versions.sqldelight}",
          'runtime': [
              'common': "com.squareup.sqldelight:runtime-metadata:${versions.sqldelight}",
              'jdk': "com.squareup.sqldelight:runtime-jvm:${versions.sqldelight}",
          ],
      ],
  ]

  ext.gradlePlugins = [
      'android': "com.android.tools.build:gradle:${versions.android}",
      'kotlin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
      'sqldelight': "com.squareup.sqldelight:gradle-plugin:${versions.sqldelight}"
  ]

  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath gradlePlugins.android
    classpath gradlePlugins.kotlin
    classpath gradlePlugins.sqldelight
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
